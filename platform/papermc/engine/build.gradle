/**
 * MCEconomy Engine Module
 * Primary implementation of the MCEconomy system for PaperMC.
 */
plugins {
    id 'java-library'
    id 'maven-publish'
}

version = project.property('mceconomy-engine.version')

dependencies {
    api project(':api')
    api project(':common')
    compileOnly 'io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT'
}

// Define variables for cleaner URL construction
def gitOwner = project.property('git-org.name')
def gitRepo = project.property('git-org.repository')
def gitUrl = project.property('mceconomy-engine-artifact.url')

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = project.property('mceconomy-engine-artifact.id')
            artifact shadowJar

            pom {
                name = project.property('mceconomy-engine.name')
                description = project.property('mceconomy-engine-artifact.description')
                url = gitUrl

                developers {
                    developer {
                        id = 'jetsadawijit'
                        url = 'https://jetsadawijit.github.io'
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/${gitOwner}/${gitRepo}.git"
                    developerConnection = "scm:git:ssh://github.com/${gitOwner}/${gitRepo}.git"
                    url = gitUrl
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${gitOwner}/${gitRepo}")

            credentials {
                username = System.getenv("USER_GITHUB_NAME")
                password = System.getenv("USER_GITHUB_TOKEN")
            }
        }
    }
}

/**
 * Process resources (plugin.yml) and replace placeholders with gradle properties.
 */
processResources {
    def props = [
        'engine_name'       : project.property('mceconomy-engine.name'),
        'project_group'     : project.group,
        'engine_description': project.property('mceconomy-engine-artifact.description'),
        'engine_version'    : project.version,
        'engine_website'    : project.property('mceconomy-engine-artifact.url')
    ]
    inputs.properties(props)
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
